/*
 * RunnerBe © 2022 Team AppleMango. all rights reserved.
 * RunnerBe license is under the MIT.
 *
 * [common.gradle] created by Ji Sungbin on 22. 2. 1. 오전 11:11
 *
 * Please see: https://github.com/applemango-runnerbe/RunnerBe-Android/blob/main/LICENSE.
 */

def hasLibraryPlugin = pluginManager.hasPlugin("com.android.library")
def hasApplicationPlugin = pluginManager.hasPlugin("com.android.application")
def hasDFMPlugin = pluginManager.hasPlugin("com.android.dynamic-feature")
def hasScabbardPlugin = pluginManager.hasPlugin("scabbard.gradle")

if (hasLibraryPlugin || hasApplicationPlugin || hasDFMPlugin) {
    if (hasScabbardPlugin) {
        scabbard {
            enabled = true
            failOnError = true
            fullBindingGraphValidation = true
        }
    }

    android {
        compileSdk = ApplicationConstants.compileSdk

        defaultConfig {
            minSdk = ApplicationConstants.minSdk
            targetSdk = ApplicationConstants.targetSdk

            if (!hasDFMPlugin) {
                multiDexEnabled = true
            }
        }

        sourceSets {
            main.java.srcDirs += 'src/main/kotlin/'
        }

        compileOptions {
            sourceCompatibility = ApplicationConstants.javaVersion
            targetCompatibility = ApplicationConstants.javaVersion
        }

        kotlinOptions {
            jvmTarget = ApplicationConstants.jvmTarget
        }

        testOptions {
            unitTests.all {
                useJUnitPlatform()
            }
        }
    }
}
